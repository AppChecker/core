configurations {
    compile
    provided
    plugins
    felix
    starterContent
    coreWeb
    tomcatLibs
}

configurations.all({
    // dynamic: e.g. 'version: 2.0+'
    resolutionStrategy.cacheDynamicVersionsFor 1, 'hours'
    // changing: e.g. 'version 2.0.0-SNAPSHOT'
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
})

repositories {
    maven { url "http://repo.dotcms.com/artifactory/libs-release" }
    maven { url "http://repo.dotcms.com/artifactory/libs-snapshot-local" }
}

task cleanPre3_5_Files(type: Delete) {
    delete "${project.projectDir}/dotCMS/WEB-INF/lib",
            "${project.projectDir}/dotCMS/WEB-INF/classes",
            "${project.projectDir}/dotCMS/WEB-INF/felix/bundle",
            "${project.projectDir}/dotCMS/starter.zip",
            "${project.projectDir}/dotCMS/html/js/_rulesengine"
}

task listProperties() {
    description = 'Write out all the project properties at info log level.'
    doLast {
        logger.info("Project properties for ':${project.name}'")
        project.ext.properties.sort().each { k, v -> logger.info "\t$k\n\t\t$v" }
    }
}

def usePre3_5Build(){
    logger.info("Building with pre3.5 Ant build system.")
    //Import and apply the dependencies from the dependencies scripts
    apply from: "ant.gradle"
    apply from: "$rootDir/build-pre3_5-ant.gradle"
    clean.dependsOn cleanPre3_5_Files
}

def usePureGradleBuild(){
    logger.info("Building with incubating Gradle build: ")
    apply from: "${rootDir}/core.gradle"
    clean.dependsOn cleanPre3_5_Files
    move_war_jboss7.dependsOn undeploy_war_jboss7
}
def useNewHotness = project.hasProperty('useNewBuild') ? project['useNewBuild'] != 'false' : project['use-pre-3_5-build'] == 'false'
if(useNewHotness){
    usePureGradleBuild()
} else {
    usePre3_5Build()
    clean.dependsOn('clean_tinymce_gz')
    deploy_war_jboss7.dependsOn(clone_pull_jboss7)
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

defaultTasks 'help', 'tasks'

